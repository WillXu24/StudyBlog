用汇编程序实现以下伪代码，要求采用移位指令实现乘除法运算。

```c
int main() 
{
int K, Y ; 
int Z[50] ; 
Y = 56;
for(K=0;K<50;K++) 
Z[K] = Y - 16 * ( K / 4 + 210 ) ;
}
```

<!--more-->

## 一、任务分析

### 1. 需要用到的数据类型

- 立即数

- 字（4字节）

- 字数组

### 2. 需要的汇编语言类型

- 构成循环的小于设置指令（slt）、相等转移指令（beq）和跳转指令（j）

- 基本运算指令（add、sub），移位指令（sll、srl）

- 数据传递指令（lw、sw）

### 3. 基本思路

-  分配数组空间

- 定义K、Y，存储数组初始地址

-  建立循环条件

- 运算赋值并写入内存

## 二、源码

```assembly
.data 
z:.space 200 #分配数组空间
 
.text
main:
la $t0, z  #数组z初始地址->to
li $t1, 0  #定义并赋值K->t1
li $t2, 56 #定义并赋值Y->t2

loop:
slti $t3, $t1, 50   #判断k是否于50,k<50,t3=1
beq $t3, $0, exit   #当k大于等于50, 跳转退出
srl $t4, $t1, 2     #k/4
addi $t4, $t4, 210  #k/4+210
sll $t4, $t4, 4     #16*（k/4+210）
sub $t4, $t2, $t4   #y-16*（k/4+210）
sw $t4, 0($t0)      #写入z［k］
addi $t0, $t0, 4    #指向数组下一位
addi $t1, $t1, 1    #k+1
j loop              #循环

exit:
li $v0, 10
syscall
```

##  三、 运行结果

### 1. 代码段内存映像

![](https://i.loli.net/2019/04/12/5cb01a83e0bef.png)

### 2. 数据段内存映像

![](https://i.loli.net/2019/04/12/5cb01a9516097.png)

## 四、 问题及改进

### 1.  存在的问题

在qtspim中无法在窗口实现输出

### 2. 代码改进

```assembly
.data 
.align 2 
z:.space 200 #分配数组空间
str:.asciiz "Z[" 
str1:.asciiz "]=" 
str2:.asciiz "\n" 
.globl main 

.text 
main: 
la $t0,z    #数组z初始地址->to
li $t1,0    #定义并赋值K->t1
li $t2,56   #定义并赋值Y->t2
loop:
slti $t3, $t1, 50   #判断k是否于50,k<50,t3=1
beq $t3, $0, exit   #当k大于等于50, 跳转退出
srl $t4, $t1, 2     #k/4
addi $t4, $t4, 210  #k/4+210
sll $t4, $t4, 4     #16*（k/4+210）
sub $t4, $t2, $t4   #y-16*（k/4+210）
sw $t4, 0($t0)      #写入z［k］
addi $t0, $t0, 4    #指向数组下一位

li $v0,4 
la  $a0,str 
syscall             #输出"Z[" 
li $v0,1 
add $a0,$0,$t1 
syscall             #k 
li $v0,4 
la $a0,str1         #输出"]=" 
syscall 
li $v0,1 
add $a0,$0,$t4
syscall             #输出数组值
li $v0,4 
la $a0,str2         #换行
syscall 
addi $t1, $t1, 1    #k+1
j loop              #循环
exit:
li $v0, 10
syscall
```